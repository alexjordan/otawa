// canonicals
canon s32 "_R"(u32)
canon s32 "_S"(u32) 
canon bit "_P"(u32) 
canon u32 "_MCB"

canon "_NOP"()
canon "_BRANCH"(u32)
canon "_TRAP"()
canon "_CONT"()
canon "_IF"(u32, u32, u32)
canon "_LOAD"(u32, u32, u32)
canon "_STORE"(u32, u32, u32)
canon "_SCRATCH"(u32)
canon "_SET"(u32, u32)
canon "_SETI"(u32, s32)
canon "_CMP"(u32, s32, s32)
canon "_CMPU"(u32, u32, u32)
canon "_ADD"(u32, u32, u32)
canon "_SUB"(u32, u32, u32)
canon "_SHL"(u32, u32, u32)
canon "_SHR"(u32, u32, u32)
canon "_ASR"(u32, u32, u32)
canon "_NOT"(u32, u32)
canon "_AND"(u32, u32, u32)
canon "_OR"(u32, u32, u32)
canon "_MUL"(s32, s32, s32)
canon "_MULU"(u32, u32, u32)
canon "_XOR"(u32, u32, u32)

// condition codes
canon u32 "_NO_COND"
canon u32 "_EQ"
canon u32 "_LT"
canon u32 "_LE"
canon u32 "_GE"
canon u32 "_GT"
canon u32 "_ANY_COND"
canon u32 "_NE"
canon u32 "_ULT"
canon u32 "_ULE"
canon u32 "_UGE"
canon u32 "_UGT"

// memory types
canon u32 "_INT8"
canon u32 "_INT16"
canon u32 "_INT32"
canon u32 "_UINT8"
canon u32 "_UINT16"
canon u32 "_UINT32"

// special registers
macro SZ = "_S"(0) 
macro SM = "_S"(1) 
macro SL = "_S"(2) 
macro SH = "_S"(3) 
macro SS = "_S"(5) 
macro ST = "_S"(6)
// We model the offset registers as absolute addresses here,
// not as offset to the base.
macro SRB = "_S"(7) 
macro SRO = "_S"(8) 
macro SXB = "_S"(9) 
macro SXO = "_S"(10)
macro MCB = "_MCB"

// operands
let T1 = -1
let T2 = -2
let T3 = -3
macro RD = "_R"(rd.value)
macro RA = "_R"(ra.value)
macro RS = "_R"(rs.value)
macro RS1 = "_R"(rs1.value)
macro RS2 = "_R"(rs2.value)
macro PD = "_P"(pd.value)
macro PS = "_P"(ps.value)
macro PS1 = "_P"(ps1.value)
macro PS2 = "_P"(ps2.value)

// operations
macro NOP = "_NOP"()
macro BRANCH(a) = "_BRANCH"(a)
macro TRAP = "_TRAP"()
macro CONT = "_CONT"()
macro IF(a,b,c) = "_IF"(a,b,c)
macro LOAD(d, s1, s2) = "_LOAD"(d, s1, s2)
macro STORE(d, s1, s2) = "_STORE"(d, s1, s2)
macro SCRATCH(d) = "_SCRATCH"(d)
macro SET(d, s) = "_SET"(d, s)
macro SETI(d, i) = "_SETI"(d, i)
macro CMP(d, s1, s2) = "_CMP"(d, s1, s2)
macro CMPU(d, s1, s2) = "_CMPU"(d, s1, s2)
macro ADD(d, s1, s2) = "_ADD"(d, s1, s2)
macro SUB(d, s1, s2) = "_SUB"(d, s1, s2)
macro SHL(d, s1, s2) = "_SHL"(d, s1, s2)
macro SHR(d, s1, s2) = "_SHR"(d, s1, s2)
macro ASR(d, s1, s2) = "_ASR"(d, s1, s2)
macro NOT(d, s) = "_NOT"(d, s)
macro AND(d, s1, s2) = "_AND"(d, s1, s2)
macro OR(d, s1, s2) = "_OR"(d, s1, s2)
macro MUL(d, s1, s2) = "_MUL"(d, s1, s2)
macro MULU(d, s1, s2) = "_MULU"(d, s1, s2)
macro XOR(d, s1, s2) = "_XOR"(d, s1, s2)

macro SHLI(d, s, i) = "_SETI"(T1, i); "_SHL"(d, s, T1)
macro SHRI(d, s, i) = "_SETI"(T1, i); "_SHR"(d, s, T1)
macro ASHRI(d, s, i) = "_SETI"(T1, i); "_ASR"(d, s, T1)
macro ADDI(d, s, i) = "_SETI"(T1, i); "_ADD"(d, s, T1)
macro SUBI(d, s, i) = "_SETI"(T1, i); "_SUB"(d, s, T1)
macro ANDI(d, s, i) = "_SETI"(T1, i); "_AND"(d, s, T1)
macro XORI(d, s, i) = "_SETI"(T1, i); "_XOR"(d, s, T1)

// temporaries
var t1[1, u32]
var t2[1, u32]
var t3[1, u32]

// Workaround for D-SPM address space aliasing: We map the D-SPM into
// a separate address range in global memory for the data-flow analysis.
// D-SPM can only accessed via lwl and swl, so there will be no issues with
// other memory operations, but it may has to be taken into account if value-facts 
// are provided/used.
let SPM_BASE_ADDR = 0x80000000

extend reg_idx
	value = i

extend sreg_idx
	value = i

extend pred_idx
	value = i

extend npred_idx, gpred_idx
	value = i
	negated = n

extend ALU_op
	opcode = opc

extend ALU_extop
	opcode = opc

extend ALUc_op
	opcode = opc

extend ALUp_op
	opcode = opc

// Note: This macro uses T1!
macro GET_PRED(d, p) = \
        if p.negated then \
	  XORI(d, "_P"(p.value), 1); \
	else \
          SET(d, "_P"(p.value)); \
	endif

macro SET_PRED(p, s, op) = SETI(p, 0); IF(s, op, 1); SETI(p, 1)


// ---- All Predicated Instructions ----

extend Pred_fmt
	otawa_sem = {
	  if !guard.always_true then
	    GET_PRED(T2, guard);
	    SETI(T1, 1);
	    CMP(T1, T2, T1);
	    IF(T1, "_NE", 1);
	    CONT;
	  endif;
	  x.otawa_sem;
	}


// ---- ALU operations ----

// Note: this macro uses T1 and T2
macro ALU_semop(opc, d, s1, s2) = \
	  switch (opc) { \
	    case 0: ADD(d, s1, s2); \
	    case 1: SUB(d, s1, s2); \
	    case 2: XOR(d, s1, s2); \
	    case 3: SHL(d, s1, s2); \
	    case 4: SHR(d, s1, s2); \
	    case 5: ASR(d, s1, s2); \
	    case 6:  OR(d, s1, s2); \
	    case 7: AND(d, s1, s2); \
	    case 11: OR(T1, s1, s2); NOT(d, T1); \
	    case 12: SHLI(T1, s1, 1); ADD(d, T1, s2); \
	    case 13: SHLI(T1, s1, 1); ADD(d, T1, s2); \
	  }

// Note: Uses T1 and T2
macro ALUc_semop(opc, p, s1, s2) = \
        switch (opc) { \
          case 0: CMP(T1, s1, s2); SET_PRED(p, T1, "_EQ"); \
	  case 1: CMP(T1, s1, s2); SET_PRED(p, T1, "_NE"); \
	  case 2: CMP(T1, s1, s2); SET_PRED(p, T1, "_LT"); \
	  case 3: CMP(T1, s1, s2); SET_PRED(p, T1, "_LE"); \
	  case 4: CMP(T1, s1, s2); SET_PRED(p, T1, "_ULT"); \
	  case 5: CMP(T1, s1, s2); SET_PRED(p, T1, "_ULE"); \
	  case 6: SETI(T1, 1); SHL(T1, T1, s2); AND(T1, s1, T1); \
	          SETI(T2, 0); CMP(T1, T1, T2); SET_PRED(p, T1, "_NE"); \
	}

macro ALUp_semop(opc, p, p1, p2) = \
        switch (opc) { \
	  case 6:   OR(T1, p1, p2); SET(p, T1); \
	  case 7:  AND(T1, p1, p2); SET(p, T1); \
	  case 10: XOR(T1, p1, p2); SET(p, T1); \
	}

extend ALUi_fmt
	otawa_sem = {
	  if !rd.is_zero then
	    SETI(T2, imm12);
	    ALU_semop(func.opcode, RD, RS1, T2);
	  endif;
	}

extend ALUl_fmt
	otawa_sem = {
	  if !rd.is_zero then
	    SETI(T2, imm32);
	    ALU_semop(func.opcode, RD, RS1, T2);
	  endif;
	}

extend ALUr_fmt
	otawa_sem = {
	  if !rd.is_zero then
	    ALU_semop(func.opcode, RD, RS1, RS2);
	  endif;
	}

extend ALUm_fmt
	otawa_sem = {
	  if func.is_unsigned then
	    MULU(SL, RS1, RS2);
	  else
	    MUL(SL, RS1, RS2);
	  endif;
	  // For now, do not calculate the high word..
	  SCRATCH(SH);
	}

extend ALUc_fmt
	otawa_sem = {
	  ALUc_semop(func.opcode, PD, RS1, RS2);
	}

extend ALUci_fmt
	otawa_sem = {
	  SETI(T2, imm5);
	  ALUc_semop(func.opcode, PD, RS1, T2);
	}

extend ALUp_fmt
	otawa_sem = {
	  GET_PRED(T1, ps1);
	  GET_PRED(T2, ps2);
	  ALUp_semop(func.opcode, PD, T1, T2);
	}

extend ALUb_fmt
	otawa_sem = {
	  // rd = (rs1 & (1 << imm5)) | (ps << imm5)
	  GET_PRED(T3, ps);
	  SETI(T1, 1);
	  SETI(T2, imm5);
	  SHL(T1, T1, T2);
	  NOT(T1, T1);
	  AND(T1, T1, RS1);
	  SHL(T3, T3, T2);
	  OR(RD, T1, T3);
	}


// ---- Special Instructions -----

extend SPCw_fmt
	otawa_sem = {
	  // Just waits..
	}

extend SPCt_fmt
	otawa_sem = {
	  SET("_S"(sd.value), RS1);
	}

extend SPCf_fmt
	otawa_sem = {
	  SET(RD, "_S"(ss.value));
	}


// ---- Load and Store ----

// This macro uses T1
macro MEM_ADDR(d, ra, imm) = \
	SETI(d, imm); \
	if func.is_word then \
	  SHL(d, d, 2); \
	else \
	  if func.is_half then \
	    SHL(d, d, 1); \
	  endif; \
	endif; \
	ADD(d, d, ra); \
	if func.is_stack then \
	  ADD(d, d, ST); \
	endif; \
	if func.is_local then \
	  // We could also use SETP here to map local accesses to \
	  // a different page. \
	  ADDI(d, d, SPM_BASE_ADDR); \
	endif

macro get_mem_type(func) = \
	if func.is_word then \
	  "_UINT32" \
	else \
	  if func.is_half then \
	    if func.is_unsigned then "_UINT16" else "_INT16" endif \
          else \
	    if func.is_unsigned then "_UINT8" else "_INT8" endif \
	  endif \
	endif

extend LDT_fmt
	otawa_sem = {
	  MEM_ADDR(T2, RA, imm7);
	  LOAD(RD, T2, get_mem_type(func));
	}

extend STT_fmt
	otawa_sem = {
	  MEM_ADDR(T2, RA, imm7);
	  STORE(T2, RS, get_mem_type(func));
	}


// ---- Stack Control ----

extend STC_op
	opcode = opc

// We only track ST pointer for now..
macro STC_semop(opc, s) = \
        switch (opc) { \
	  case 0: SUB(ST, ST, s); SCRATCH(SS); \
	  case 1: SCRATCH(SS); \
	  case 2: ADD(ST, ST, s); SCRATCH(SS); \
	  case 4: SCRATCH(SS); \
	}

extend STCi_fmt
	otawa_sem = {
	  SETI(T2, imm18);
	  STC_semop(func.opcode, T2);
	}

extend STCr_fmt
	otawa_sem = {
	  STC_semop(func.opcode, RS1);
	}


// ---- Control Flow ----

macro CFL_semop(func, a, b) = \
	if func.is_trap then \
	  SET(SXB, MCB); \
	  SCRATCH(SXO); \
	  TRAP; \
	else \
	  if func.is_call then \
	    SET(SRB, MCB); \
	    // We do not calculate the return address for now \
	    SCRATCH(SRO); \
	  endif; \
	  BRANCH(a); \
	endif; \
	if !func.is_local then \
	  SET(MCB, b); \
	endif

extend CFLi_fmt
	otawa_sem = {
	  SETI(T1, imm22);
	  SETI(T2, 2);
	  SHL(T1, T1, T2);
	  if func.is_PCRel then
	    ADDI(T1, T1, __IADDR);
	  endif;
	  CFL_semop(func, T1, T1);
	}

extend CFLri_fmt
	otawa_sem = {
	  if func.is_xret then
	    SET(MCB, SXB);
	    BRANCH(SXO);
	  else
	    SET(MCB, SRB);
	    BRANCH(SRO);
	  endif;
	}

extend CFLrs_fmt
	otawa_sem = {
	  CFL_semop(func, RS1, RS1);
	}

extend CFLrt_fmt
	otawa_sem = {
	  ADD(T1, RS1, RS2);
	  CFL_semop(func, T1, RS1);
	}

